// Julia set in ASCII art

var wi = 40
var hi = 30
var max-iterations = 30
var c_re = -0.7
var c_im = 0.27015
var charset = (list "@" "W" "%" "&" "$" "#" "O" "I" "o" "+" "=" "-" "^" "," "." " " )

var w = (int.to-double wi)
var h = (int.to-double hi)
var half-w = [0.5 * w]
var half-h = [0.5 * h]

var old_re = 0.
var old_im = 0.
var image = (list)

for yi from 0 to [hi - 1] {
   var y = (int.to-double yi)
   for xi from 0 to [wi - 1] {
      var x = (int.to-double xi)
      var new_re = [[1.5 * [x - half-w]] / half-w]
      var new_im = [[y - half-h] / half-h]
      var stop = false
      var i = 0
      while (not stop) {
         set old_re = new_re
         set old_im = new_im
         set new_re = [[[old_re ** 2.] - [old_im ** 2.]] + c_re]
         set new_im = [[[2. * old_re] * old_im] + c_im]
         if [[[new_re ** 2.] + [new_im ** 2.]] > 4.] {
            set stop = true
         }
         if [i >= max-iterations] {
            set stop = true
         } else {
            set i = [i + 1]
         }
      }
      [image += [i / 2]]
   }
   [image += false]
}

for x in image {
   (print match x {
      case Int: [charset @ x]
      case false: "\n"
   })
}
